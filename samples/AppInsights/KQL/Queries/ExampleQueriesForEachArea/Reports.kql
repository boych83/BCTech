// KQL samples - Report execution

// successful report execution (raw data)
traces
| where timestamp > ago(60d) // adjust as needed
| where operation_Name == "Success report generation" // do note that in a later version of the schema, this field will not be used 
     or customDimensions.eventId == 'RT0006' // introduced in version 16.1
| where customDimensions.result == "Success"
| project timestamp
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, companyName = customDimensions.companyName
, extensionId = customDimensions.extensionId
, extensionName = customDimensions.extensionName
, extensionVersion = customDimensions.extensionVersion
, extensionPublisher = customDimensions.extensionPublisher
, alObjectId = customDimensions.alObjectId
, alObjectName = customDimensions.alObjectName
, alObjectType = customDimensions.alObjectType
, alStackTrace = customDimensions.alStackTrace
// documentFormat dimension added in version 20.0
, documentFormat = customDimensions.documentFormat
// layout dimensions added in version 20.0
, LayoutAppId = customDimensions.layoutAppId
, LayoutName = customDimensions.layoutName
, LayoutType = customDimensions.layoutType
, numberOfRows = customDimensions.numberOfRows
// reportAction dimension added in version 20.0
, reportAction = customDimensions.reportAction
// reportingEngine dimension was added in version 17.3
, reportingEngine = customDimensions.reportingEngine
, result = customDimensions.result
, serverExecutionTime = customDimensions.serverExecutionTime
, serverExecutionTimeInMS = toreal(totimespan(customDimensions.serverExecutionTime))/10000 //the datatype for executionTime is timespan 
// sqlDatabaseAccessIntent dimension added in version 19.1
, sqlDatabaseAccessIntent = customDimensions.sqlDatabaseAccessIntent 
, sqlExecutes = customDimensions.sqlExecutes 
, sqlRowsRead = customDimensions.sqlRowsRead
, totalTime = customDimensions.totalTime
, totalTimeInMS = toreal(totimespan(customDimensions.totalTime))/10000 //the datatype for executionTime is timespan 
, usertelemetryId = case(
  // user telemetry id was introduced in the platform in version 20.0
  toint( substring(customDimensions.componentVersion,0,2)) >= 20, user_Id
, 'N/A'
)
| extend renderTimeInMS = totalTimeInMS - serverExecutionTimeInMS

// failed report execution (raw data)
traces
| where timestamp > ago(60d) // adjust as needed
| where operation_Name == "Failed report generation" // do note that in a later version of the schema, this field will not be used 
  or customDimensions.eventId == 'RT0006' // introduced in version 16.1
| where customDimensions.result <> "Success"
| extend aadID 
| project timestamp
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, companyName = customDimensions.companyName
, extensionId = customDimensions.extensionId
, extensionName = customDimensions.extensionName
, extensionVersion = customDimensions.extensionVersion
, extensionPublisher = customDimensions.extensionPublisher
, alObjectId = customDimensions.alObjectId
, alObjectName = customDimensions.alObjectName
, alObjectType = customDimensions.alObjectType
, alStackTrace = customDimensions.alStackTrace
, failureReason = customDimensions.result
, numberOfRows = customDimensions.numberOfRows
// reportingEngine dimension added in version 17.3
, reportingEngine = customDimensions.reportingEngine
// reportAction dimension added in version 20.0
, reportAction = customDimensions.reportAction
// documentFormat dimension added in version 20.0
, documentFormat = customDimensions.documentFormat
, serverExecutionTime = customDimensions.serverExecutionTime
, serverExecutionTimeInMS = toreal(totimespan(customDimensions.serverExecutionTime))/10000 //the datatype for executionTime is timespan 
, totalTime = customDimensions.totalTime
, totalTimeInMS = toreal(totimespan(customDimensions.totalTime))/10000 //the datatype for executionTime is timespan 
, usertelemetryId = case(
  // user telemetry id was introduced in the platform in version 20.0
  toint( substring(customDimensions.componentVersion,0,2)) >= 20, user_Id
, 'N/A'
)
| extend renderTimeInMS = totalTimeInMS - serverExecutionTimeInMS


// cancelled report execution (raw data)
traces
| where timestamp > ago(60d) // adjust as needed
| where operation_Name == "Cancellation report generation" // do note that in a later version of the schema, this field will not be used   
     or customDimensions.eventId == "RT0007" // introduced in version 16.1
| project timestamp
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, companyName = customDimensions.companyName
, extensionId = customDimensions.extensionId
, extensionName = customDimensions.extensionName
, extensionVersion = customDimensions.extensionVersion
, alObjectId = customDimensions.alObjectId
, alObjectName = customDimensions.alObjectName
, alObjectType = customDimensions.alObjectType
, alStackTrace = customDimensions.alStackTrace
, cancelReason = case(
  message contains "The number of processed rows exceeded", "MaxRowsExceeded"
, message contains "The action took longer to complete", "MaxTimeExceeded"
, message contains "Received a cancellation request from the user", "UserCancelled"
, "unknown"
)
// reportingEngine dimension added in version 17.3
, reportingEngine = customDimensions.reportingEngine
// reportAction dimension added in version 20.0
, reportAction = customDimensions.reportAction
// documentFormat dimension added in version 20.0
, documentFormat = customDimensions.documentFormat
, usertelemetryId = case(
  // user telemetry id was introduced in the platform in version 20.0
  toint( substring(customDimensions.componentVersion,0,2)) >= 20, user_Id
, 'N/A'
)


// cancelled report execution, where COMMIT occurred (raw data)
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'RT0011'
| project timestamp
, aadTenantId = tostring( customDimensions.aadTenantId )
, environmentName = customDimensions.environmentName
, environmentType = tostring( customDimensions.environmentType )
, companyName = customDimensions.companyName
, cancelReason = tostring( customDimensions.cancelReason )
, component = customDimensions.component 
, componentVersion = customDimensions.componentVersion 
, clientType = customDimensions.clientType
, extensionId = customDimensions.extensionId
, extensionPublisher = customDimensions.extensionPublisher
, extensionName = customDimensions.extensionName
, extensionVersion = customDimensions.extensionVersion
, alObjectId = customDimensions.alObjectId
, alObjectName = customDimensions.alObjectName
, alObjectType = customDimensions.alObjectType
, alStackTrace = customDimensions.alStackTrace
// reportingEngine dimension added in version 17.3
, reportingEngine = customDimensions.reportingEngine
// reportAction dimension added in version 20.0
, reportAction = customDimensions.reportAction
// documentFormat dimension added in version 20.0
, documentFormat = customDimensions.documentFormat
, usertelemetryId = case(
  // user telemetry id was introduced in the platform in version 20.0
  toint( substring(customDimensions.componentVersion,0,2)) >= 20, user_Id
, 'N/A'
)